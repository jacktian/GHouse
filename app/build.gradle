apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def appConfiguration = rootProject.extensions.getByName("ext")
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

Properties properties = new Properties();
File gradlePropertiesFile = project.file('../gradle.properties')
if(gradlePropertiesFile.exists()){
    properties.load(gradlePropertiesFile.newDataInputStream())
}else {
    println("File gradlePropertiesFile doesn't exist!")
}

def CLIENT_ID = properties.getProperty("CLIENT_ID")
def CLIENT_TOKEN = properties.getProperty("CLIENT_TOKEN")
def API_ENDPOINT = properties.getProperty("API_ENDPOINT")
def API_VERSION = properties.getProperty("API_VERSION")
def UMENG_APPKEY = properties.getProperty("UMENG_APPKEY")

android {
    compileSdkVersion appConfiguration.getAt("ANDROID_COMPILE_SDK_VERSION") as int
    buildToolsVersion appConfiguration.getAt("ANDROID_BUILD_TOOLS_VERSION")

    sourceSets.main {
        jniLibs.srcDirs = ['libs']
    }

    defaultConfig {
        applicationId "co.lujun.ghouse"
        minSdkVersion appConfiguration.getAt("ANDROID_BUILD_MIN_SDK_VERSION") as int
        targetSdkVersion appConfiguration.getAt("ANDROID_TARGET_SDK_VERSION") as int
        versionCode appConfiguration.getAt("VERSION_CODE") as int
        versionName appConfiguration.getAt("VERSION_NAME")

        buildConfigField "String", "CLIENT_ID", "\"${CLIENT_ID}\""
        buildConfigField "String", "CLIENT_TOKEN", "\"${CLIENT_TOKEN}\""
        buildConfigField "String", "API_ENDPOINT", "\"${API_ENDPOINT}\""
        buildConfigField "String", "API_VERSION", "\"${API_VERSION}\""

        multiDexEnabled true
    }

    signingConfigs {
        debug {
        }
        release {
            def signingFile = file('../signing.properties')
            if(signingFile.canRead()){
                def Properties props = new Properties()
                props.load(new FileInputStream(signingFile))

                try {
                    storeFile = file( props['STORE_FILE'] )
                    storePassword = props['STORE_PASSWORD']
                    keyAlias = props['KEY_ALIAS']
                    keyPassword = props['KEY_PASSWORD']

                    println "RELEASE_BUILD: Signing..."
                }catch (e){
                    throw new InvalidUserDataException("You should define STORE_FILE and STORE_PASSWORD and KEY_ALIAS and KEY_PASSWORD in signing.properties.")
                }
            }else{
                println "RELEASE_BUILD: signing.properties not found"
            }
        }
    }

    buildTypes {
        debug {
            // call BuildConfig.LOG_DEBUG
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-dev"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            //don't show Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            //remove unuse resources files
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "Sense_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        GHouse {}
//        Mi {}
//        Meizu {}
//        Yingyongbao {}
//        app360 {}
//        Wandoujia {}
//        Fir {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL: name, UMENG_APPKEY: UMENG_APPKEY]
    }

    /*variantFilter { variant ->
        def buildType = variant.buildType.name
        def flavorName = variant.getFlavors().get(0).name
        if (buildType.equals('debug')) {
            if (flavorName.equals('development')) {
                variant.setIgnore(false)
            } else {
                variant.setIgnore(true)
            }
        } else {
            if (flavorName.equals('development')) {
                variant.setIgnore(true)
            } else {
                variant.setIgnore(false)
            }
        }
    }*/

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def SUPPORT_VERSION = appConfiguration.getAt("ANDROID_BUILD_TOOLS_VERSION")
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:' + SUPPORT_VERSION
    compile 'com.android.support:recyclerview-v7:' + SUPPORT_VERSION
    compile 'com.android.support:cardview-v7:' + SUPPORT_VERSION
    compile 'com.android.support:design:22.2.0'
    compile 'com.github.whilu.CustomButton:library:1.0.0'
    compile 'com.github.rey5137:material:1.2.1'
    compile 'com.github.traex.rippleeffect:library:1.3'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'co.lujun:androidelasticview:1.0.9'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    compile 'com.qiniu:qiniu-android-sdk:7.0.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
}

retrolambda {
//    jdk System.getenv("JAVA8_HOME")
//    oldJdk System.getenv("JAVA6_HOME")
    javaVersion JavaVersion.VERSION_1_6
//    jvmArgs '-arg1', '-arg2'
//    defaultMethods false
//    incremental true
}
