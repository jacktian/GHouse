buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        //use retrolambda
        classpath 'me.tatarka:gradle-retrolambda:3.2.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))}

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "co.lujun.ghouse"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 101
        versionName "1.0.1"

        //dexMethod 65535
        multiDexEnabled true
        //default flavor
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "GHouse"]
    }
    lintOptions{
        abortOnError false
    }

    signingConfigs {
        debug{
            //
        }
        release {
            keyAlias 'lujun'
            keyPassword KEYPASSWORD
            storeFile file('E:/Code/Android/Keystore/lujun.keystore')
            storePassword STOREPASSWORD
        }
    }

    buildTypes {
        debug {
            //don't show Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            //don't show Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            //remove unuse resources files
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all{variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // output apk name ghouse_v1.0.1_2015-01-15_Wandoujia.apk
                        def fileName = "ghouse_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        GHouse {}
        Mi {}
        Meizu {}
        Yingyongbao {}
        app360 {}
        Wandoujia {}
        productFlavors.all {
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.github.whilu.CustomButton:library:1.0.0'
    compile 'com.github.rey5137:material:1.2.1'
    compile 'com.github.traex.rippleeffect:library:1.3'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    compile 'co.lujun:androidelasticview:1.0.9'
//    compile 'co.lujun:tpsharelogin:1.0.2'
    compile 'com.qiniu:qiniu-android-sdk:7.0.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
}

retrolambda {
//    jdk System.getenv("JAVA8_HOME")
//    oldJdk System.getenv("JAVA6_HOME")
    javaVersion JavaVersion.VERSION_1_6
//    jvmArgs '-arg1', '-arg2'
//    defaultMethods false
//    incremental true
}
